#lang typed/racket

;; this file combines the hashes specified in the crosslisting files,
;; that were generated by the HTML scraper.

;; these file numbers have no leading zeros.

;; the crosslisting resolution here is clearly wrong WRT the
;; canonical listing used by the registrar; For instance,
;; CPE 344 should resolve to CSC 344 and not vice versa.
;; I've changed this one manually....

(require racket/runtime-path)

(require/typed racket/hash
               [hash-union
                ((HashTable Course Course)
                 (HashTable Course Course)
                 #:combine/key (Course Course Course -> Course)
                 ->
                 (HashTable Course Course))])

(provide crosslisting-hash
         Course
         SubjStr
         #;canonicalize-course)

(define-type Course (List SubjStr String))
(define-type SubjStr
  (U "EE" "CPE" "CSC" "LAES" "ART" "HNRS" "IME" "MATE" "DATA" "ENGR" "ME"))

(define-runtime-path HERE ".")

(define crosslisting-hash-file-subjs
  '("csc" "cpe"))

;; add a binding from every key to itself
(: add-value-self-mappings ((HashTable Course Course) -> (HashTable Course Course)))
(define (add-value-self-mappings ht)
  (for/fold ([ht ht])
            ([val (in-list (hash-values ht))])
    (when (and (hash-has-key? ht val)
               (not (equal? (hash-ref ht val) val)))
      (error 'add-value-self-mappings
             "conflict in adding self-mapping for value: ~e" val))
    (hash-set ht val val)))

(define crosslisting-hash
  (add-value-self-mappings
   (hash-set
    (for/fold : (HashTable Course Course)
      ([ht (ann (hash) (HashTable Course Course))])
      ([subj (in-list crosslisting-hash-file-subjs)])
      (hash-union
       ht
       (file->value
        (build-path HERE
                    (~a "calpoly-2015-2017-catalog-"subj"-crosslistings.rktd")))
       #:combine/key
       (Î» ([key : Course] [val1 : Course] [val2 : Course])
         (cond [(equal? val1 val2) val1]
               [else (error 'crosslisting
                            "conflicting bindings ~e and ~e for key ~e"
                            val1 val2 key)]))))
    (ann '("CPE" "202") Course)
    (ann '("CPE" "202") Course))))
